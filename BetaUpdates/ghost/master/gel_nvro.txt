//******************************************************************************
// NVRO Responses and Functions
// This really won't come into play until late in the ghost, at high friendship, but there will probably be some mid-level shenanigans.
// When the user gets Gel to go rogue, 
//******************************************************************************

NVRO.name{
	if nvrosentient{
		"Invira"
	}else{
		"NVRO"
	}
	
}

//Some small changes to the menuitem function, just since we don't need as much.
NVRO.MenuItem{
	//set up, give it the function it will go to
	"\__q[%_argv[1]"
	--
	//send references (_n is the num of total args)
	_s = ""
	for _i = 2 ; _i < _n ; _i++ {
		_s += "," + AYATEMPLATE.EscapeText(_argv[_i])
	//	_s += "," + _argv[_i]
	}
	_s
	--
	//close reference sending, display option text, and close selection text area
	"]%_argv[0]\__q"

}
//Allows NVRO.Talk to be run from an option.
OnNVRO.Talk{
	NVRO.Talk(reference0,reference1)
}

//Nvro talks in anchor text only
//_argv[0] is what they say
//_argv[1] is where it goes (if null, nonsensible)
NVRO.Talk{
	//set up, give it the function it will go to
	if _argv[1] != ""{
		"\_a[%_argv[1]"
	}else{
		"\_a[NVRO_NONSENSIBLE"
	}
	//]"
	//Those are for the sake of the code formatter
	--
	//send references (_n is the num of total args)
	_s = ""
	for _i = 2 ; _i < _n ; _i++ {
		_s += "," + AYATEMPLATE.EscapeText(_argv[_i])
	//	_s += "," + _argv[_i]
	}
	_s
	--
	//close reference sending, display option text, and close selection text area
	"]%_argv[0]\_a"
}


NVRO_NONSENSIBLE{
	nvrononsenseclicks++
	if emotion != -1 && emotion != 10{
		"%(gelLookForward)Huh? \w6%(gelConfused)What's that supposed to mean? That was %(NVRO.name) talking out of their purpose, you can leave them alone."
	}elseif emotion == 10{
		"%(gelLookForward)What the--? Oh. That%(is) %(NVRO.name) out of purpose. %(gelLookAway)%(gelFrown)Just shut up."
	}elseif emotion == -1{
		"Zzzz..."
	}
}


NVRO.GelIsAsleep{
	if nvrononsenseclicks >= 10 && qnvro == -1{
		"Are you wondering who I am? I am %(NVRO.name). %Gel is asleep\, %username."
		qnvro = 0
	}else{
		"They are asleep."
		"They're sleeping."
		"%Gel is asleep."
		if nvrononsenseclicks > 5{
			"They're sleeping\, %username."
		}
		
		if nvrononsenseclicks > 15 && qnvro > -1{
			"Are you trying to talk to me? %Gel is asleep."
			"Are you trying to talk to me? %Gel is asleep. I am a tool."
		}
	
	}
	
}


